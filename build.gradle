plugins {
    id 'org.springframework.boot' version '2.6.4' apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.google.cloud.tools.jib' version '3.2.0' apply false
}

allprojects {
    group = 'ua.tunepoint'
    version = '0.0.5'
}

subprojects {

    apply plugin: 'java-library'
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'maven-publish'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        repositories {
            maven {
                url "http://artifactory.tunepoint.fun/releases"
                allowInsecureProtocol true
            }
        }
        mavenCentral()
    }

    dependencyManagement {

        dependencies {

            dependency 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:3.1.1'
            dependency 'org.springframework.boot:spring-boot-configuration-processor:2.6.4'
            dependency 'org.springframework.boot:spring-boot-starter-data-mongodb:2.6.4'
            dependency 'org.springframework.boot:spring-boot-starter-data-jpa:2.6.4'
            dependency 'org.springframework.boot:spring-boot-starter-security:2.6.4'
            dependency 'org.springframework.boot:spring-boot-starter-web:2.6.4'
            dependency 'org.springframework.kafka:spring-kafka:2.6.4'

            dependency 'org.projectlombok:lombok:1.18.22'
            dependency 'org.hibernate.validator:hibernate-validator:6.2.2.Final'
            dependency 'org.liquibase:liquibase-core:4.9.0'
            dependency 'org.apache.commons:commons-io:1.3.2'

            dependency 'com.amazonaws:aws-java-sdk-s3:1.12.181'

            dependency 'org.mapstruct:mapstruct:1.4.2.Final'
            dependency 'org.mapstruct:mapstruct-processor:1.4.2.Final'

            dependency 'ua.tunepoint:commons-web-model:0.0.3'
            dependency 'ua.tunepoint:commons-web-exception:0.0.3'
            dependency 'ua.tunepoint:commons-security:0.0.3'
            dependency 'com.auth0:java-jwt:3.19.0'
        }
    }

    if (file('publish.gradle').exists()) {

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    groupId project.group
                    artifactId project.name
                    version project.version

                    artifact jar
                }
            }

            repositories {
                maven {
                    name  'reposilite'
                    url "http://artifactory.tunepoint.fun/${System.getenv('ARTIFACTORY_REPOSITORY')}"
                    allowInsecureProtocol true

                    credentials {
                        username System.getenv('ARTIFACTORY_USERNAME')
                        password System.getenv('ARTIFACTORY_PASSWORD')
                    }
                }
            }
        }
    }
}
